dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/c/std_prims.c)
AC_CONFIG_AUX_DIR(src/misc)
AM_INIT_AUTOMAKE(asdlGen, 1.2)
AC_PROG_MAKE_SET

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_RANLIB

AC_CHECK_PROGS(JAVAC,javac)
AC_CHECK_PROGS(SML_CM,sml-cm sml-cm.bat)

dnl find the paths to build #! shell scripts
AC_PATH_PROGS(KSH,ksh)
AC_PATH_PROGS(PERL5,perl5 perl)

dnl how to run an sml heap
eval `/bin/sh src/misc/sysname`
AC_CHECK_PROGS(RUN_SML_HEAP,sml sml-cm sml-cm.bat)

case "${RUN_SML_HEAP}" in
	sml-cm.bat) 
        RUN_SML_HEAP=`/bin/sh src/misc/find_runtime.sh`
        ;;
esac

ASDLGEN_HEAP='${top_srcdir}/src/sml/asdlGen.${HEAP_SUFFIX}'
AC_SUBST(ASDLGEN_HEAP)
AC_SUBST(HEAP_SUFFIX)

dnl Figure out how to build a zip archive 
AC_CHECK_PROGS(ZIP_PRG, zip pkzip pax)
case "${ZIP_PRG}" in
        zip | pkzip) ZIP_FLAGS="-q -0 -r"   ;;
	pax)         ZIP_FLAGS="-w -x zip -f" ;;
esac
AC_SUBST(ZIP_FLAGS)

dnl Checks for libraries.
JAVA_CLASSDIR='${prefix}/share/classes/'

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h malloc.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
dnl look at the size metrics
AC_CHECK_SIZEOF(char,0) 
AC_CHECK_SIZEOF(short,0) 
AC_CHECK_SIZEOF(int,0) 
AC_CHECK_SIZEOF(long,0)
dnl AC_CHECK_SIZEOF(long long,0)

METRICS="${ac_cv_sizeof_char}:${ac_cv_sizeof_short}:${ac_cv_sizeof_int}"
METRICS="${METRICS}:${ac_cv_sizeof_long}"
case ${METRICS} in
 1:2:4:4)   INT8="char";
            INT16="short";
            INT32="int";
            INT64="long"; 
		;;
 1:2:4:8)   INT8="char";
            INT16="short";
            INT32="int";
            INT64="long"; ;;
 *:*:4:8)   INT8="char";
            INT16="int";
            INT32="int";
            INT64="long"; ;;
 *)         INT8="char";
            INT16="short";
            INT32="int";
            INT64="long"; 
 AC_MSG_WARN([Unrecoginzed size metrics edit src/c/pkl-int.h by hand]);;
esac

AC_SUBST(INT8)
AC_SUBST(INT16)
AC_SUBST(INT32)
AC_SUBST(INT64)

dnl Check for java 
AM_CONDITIONAL(HAVE_JAVA, test -n "${JAVAC}" )

dnl Checks for library functions.
dnl Write out the files
AC_OUTPUT(Makefile src/Makefile
          src/sml/Makefile src/java/Makefile 
          src/cxx/Makefile src/c/Makefile
 	  src/misc/Makefile src/asdl/Makefile
          src/c/pkl-int.h
          src/misc/id_hack.pl src/misc/sml-batch,

dnl Set the bits on the shell scripts
[ chmod 755 src/misc/id_hack.pl src/misc/sml-batch ]
)

